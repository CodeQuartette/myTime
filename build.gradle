plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'

    //spring-rest-docs
    id 'org.asciidoctor.jvm.convert' version '3.3.2'

    //Querydsl 플러그인 사용 X 추후 배포시 gradlew 할때 에러 발생
    //id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'com.codeQuartette'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    //spring-rest-docs
    asciidoctorExt

    compileOnly {
        extendsFrom annotationProcessor
    }

    //querydsl
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    //mysql
    runtimeOnly 'com.mysql:mysql-connector-j'

    //h2
    testImplementation 'com.h2database:h2'

    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //test
    implementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //spring-rest-docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    //auth
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    //querydsl 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    //aop
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

//    // cache하다가 localdatetime 직렬화 부분 에러남
//    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
//    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}


// === QueryDsl 빌드 옵션 (선택) ===
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory  = file(querydslDir)
}

clean.doLast {
    file(querydslDir).deleteDir()
}

// === Spring-rest-docs ===
asciidoctor {
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}
